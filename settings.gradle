buildscript {
    repositories {
        maven {
            url "${artifactory_contextUrl}/plugins-release"
        }
        if (standaloneGradlePluginsVersion.contains("SNAPSHOT")) {
            mavenLocal()
            maven {
                url "${artifactory_contextUrl}/plugins-snapshot-local"
            }
        }
    }
    dependencies {
        classpath "org.labkey.build:gradlePlugins:${standaloneGradlePluginsVersion}"
     }
    configurations.all {
        // Check for updates every build for SNAPSHOT dependencies
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
}

import org.labkey.gradle.util.BuildUtils

rootProject.name="Response"

apply from: 'gradle/settings/parameters.gradle'

include ':distributions:fda'

gradle.beforeProject { project ->
    if (!BuildUtils.isStandaloneBuild(project)) {
        throw new GradleException("Please run Gradle with the provided init script ('-I init.gradle') to build outside of a standard LabKey enlistment")
    }

    project.repositories {
        mavenLocal()
        maven {
            url "${project.artifactory_contextUrl}/ext-tools-local"
        }
        maven {
            url "${project.artifactory_contextUrl}/libs-release"
        }
        maven {
            url "${project.artifactory_contextUrl}/libs-snapshot"
        }
        mavenCentral()
    }
}
